plugins {
    id 'com.palantir.docker' version '0.25.0'
    id 'org.jetbrains.kotlin.jvm' version '1.4.32'
    id 'java'
    id 'java-library'
    id 'application'
}

ext {
    sharedDir           = file("${project.rootDir}/shared")
}

group = 'com.exactpro.th2'
version = release_version

sourceCompatibility = 11
targetCompatibility = 11

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "11"
}

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenLocal()
    mavenCentral()
    jcenter()
    maven {
        name 'Sonatype_snapshots'
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        name 'Sonatype_releases'
        url 'https://s01.oss.sonatype.org/content/repositories/releases/'
    }
    maven {
        name 'Nexus'
        url 'https://nexus.exactpro.com/repository/oss-maven'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}

jar {
    manifest {
        attributes(
                'Created-By':               "${System.getProperty('java.version')} (${System.getProperty('java.vendor')})",
                'Specification-Title':      '',
                'Specification-Vendor':     'Exactpro Systems LLC',
                'Implementation-Title':     project.archivesBaseName,
                'Implementation-Vendor':    'Exactpro Systems LLC',
                'Implementation-Vendor-Id': 'com.exactpro',
                'Implementation-Version':   project.version
        )
    }
}

dependencies {
    api platform('com.exactpro.th2:bom:3.0.0')

    api "com.exactpro.th2:common:3.13.4"
    api "com.exactpro.th2:grpc-act-ssh:0.0.5"

    implementation "com.fasterxml.jackson.core:jackson-core"
    implementation "com.fasterxml.jackson.core:jackson-databind"

    implementation "org.slf4j:slf4j-log4j12"
    implementation "org.slf4j:slf4j-api"
    implementation 'org.apache.sshd:apache-sshd:2.6.0'
    implementation "org.apache.commons:commons-text:1.9"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation 'io.github.microutils:kotlin-logging:1.7.9'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testImplementation "io.strikt:strikt-core:0.28.1"
    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0"
}

test {
    useJUnitPlatform()
}

application {
    mainClassName 'com.exactpro.th2.act.bootstrap.ActMain'
}

applicationName = 'service'

distTar {
    archiveName "${applicationName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    copySpec.from(tarTree("$buildDir/distributions/${applicationName}.tar"))
}
